import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    dependencies {
        classpath 'com.pascalwelsch.gitversioner:gitversioner:0.4.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

// https://github.com/passsy/gradle-gitVersioner-plugin
apply plugin: 'com.pascalwelsch.gitversioner'
gitVersioner {
    def base = System.getenv('base')
    def snap = System.getenv('snap')
    baseBranch base == null ? 'development' : base
    def snapshot = snap == null ? true : snap != 'false'
    addSnapshot snapshot

    addLocalChangesDetails false
    if (snapshot) {
        formatter = { gitVersioner ->
            return "${gitVersioner.currentSha1Short}-SNAPSHOT"
        }
    }
}

group 'dev.markusk'
version gitVersioner.versionName

sourceCompatibility = 1.11

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
	maven {
		url 'https://nexus.markusk.dev/repository/maven-public/'
	}
}

dependencies {
    compileOnly 'dev.markusk:fetcher-api:+'

    //https://github.com/ben-manes/caffeine
    //implementation 'com.github.ben-manes.caffeine:caffeine:2.8.1'

    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.9'
    // https://mvnrepository.com/artifact/com.google.collections/google-collections
    implementation group: 'com.google.collections', name: 'google-collections', version: '1.0-rc2'
    // https://mvnrepository.com/artifact/org.liquibase/liquibase-core
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '3.8.9'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

processResources {
    filter(ReplaceTokens, tokens: [VERSION: version])
}

task sourcesForRelease(type: Copy) {
    from 'src/main/java'
    into 'build/filteredSrc'
    filter(ReplaceTokens, tokens: [VERSION: version, DATE: getBuildDate(), TIME: getBuildTime()])
}

static def getBuildDate() {
    return new Date().format('yyyy/MM/dd')
}

static def getBuildTime() {
    return new Date().format('HH:mm:ss')
}

compileJava {
    options.encoding = 'UTF-8'
    source = sourcesForRelease.destinationDir
    classpath = sourceSets.main.compileClasspath
    dependsOn sourcesForRelease
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from "${buildDir}/filteredSrc"
}

jar {
    archivesBaseName = project.name
}

shadowJar {
    archiveClassifier.set('withDependencies')
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact shadowJar
            from components.java
        }
    }
    repositories {
        maven {
            name 'deploy'
			def releasesRepoUrl = "https://nexus.markusk.dev/repository/maven-releases/"
			def snapshotsRepoUrl = "https://nexus.markusk.dev/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty('repoUser') ? project.property('repoUser') : 'user'
                password = project.hasProperty('repoPassword') ? project.property('repoPassword') : 'password'
            }
        }
    }
}